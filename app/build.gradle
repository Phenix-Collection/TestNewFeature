apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
def apkNameDate = new Date().format("yyyyMMdd_HHmmss");
android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'

    defaultConfig {
        applicationId "com.example.wenjunzhong.testnewfeature"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    productFlavors {
        googleplay {
        }
        others {
        }

        //用于压力测试
        monkey{

        }
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    android.applicationVariants.all { variant ->
        def buildType = variant.buildType
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName
                if (buildType.name.equals("debug")) {
                    fileName = "Test_v" + defaultConfig.versionName + "_" + buildType.name + "_${variant.productFlavors[0].name}" + ".apk"
                } else {
                    fileName = "Test_v" + defaultConfig.versionName + "_" + apkNameDate + "_" + buildType.name + "_${variant.productFlavors[0].name}" + ".apk"
                }
                output.outputFile = new File(outputFile.parent + "/v" + defaultConfig.versionName, fileName)
            }
        }
    }
}

dependencies {
    // 默认不添加TaklingData统计sdk
    compile fileTree(dir: 'libs', excludes: ['TalkingData_*.jar'], include: ['*.jar'])
    googleplayCompile files("libs/TalkingData_Analytics_Android_SDK_GP_V2_2_37.jar")
    othersCompile files("libs/TalkingData_Analytics_Android_SDK_V2_2_46.jar")


    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.google.firebase:firebase-ads:10.0.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha3'
    testCompile 'junit:junit:4.12'
    debugCompile 'com.github.moduth:blockcanary-android:1.2.1'
    releaseCompile 'com.github.moduth:blockcanary-no-op:1.2.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    monkeyCompile project(':leakcanary')
}

apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
}
