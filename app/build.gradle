apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.jakewharton.butterknife'
def apkNameDate = new Date().format("yyyyMMdd_HHmmss");
android {
    compileSdkVersion 24
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "com.example.wenjunzhong.testnewfeature"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    flavorDimensions "default"
    productFlavors {
        googleplay {
            dimension "default"
        }
        others {
            dimension "default"
        }

        //用于压力测试
        monkey{
            applicationId "com.example.wenjunzhong.monkey"
            dimension "default"
            versionCode 2
            versionName "2.0"
        }
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    android.applicationVariants.all { variant ->
        def buildType = variant.buildType
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName
                def prefix = "Test_v"
                def productName = variant.productFlavors[0].name
                if(variant.productFlavors[0].name == "monkey"){
                    prefix = "Monkey_v"
                }
                if (buildType.name.equals("debug")) {
                    fileName = prefix + variant.versionName + "_" + buildType.name + "_${productName}" + ".apk"
                } else {
                    fileName = prefix + variant.versionName + "_" + apkNameDate + "_" + buildType.name + "_${productName}" + ".apk"
                }
                outputFileName  = fileName
           //     output.outputFile = new File(outputFile.parent + "/v" + variant.versionName, fileName)
            }
        }
    }
}

dependencies {
    // 默认不添加TaklingData统计sdk
    compile fileTree(include: ['*.jar'], excludes: ['TalkingData_*.jar'], dir: 'libs')
    googleplayCompile files("libs/TalkingData_Analytics_Android_SDK_GP_V2_2_37.jar")
    othersCompile files("libs/TalkingData_Analytics_Android_SDK_V2_2_46.jar")
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.google.firebase:firebase-ads:10.0.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha3'
    testCompile 'junit:junit:4.12'
    debugCompile 'com.github.moduth:blockcanary-android:1.2.1'
    releaseCompile 'com.github.moduth:blockcanary-no-op:1.2.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    monkeyCompile project(':leakcanary')
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    if(UpgradeIsBuildLibrary.toBoolean()){
        compile project(':upgrade')
    }
}

apply plugin: 'com.google.gms.google-services'
